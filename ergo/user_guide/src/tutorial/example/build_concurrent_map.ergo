#!/usr/bin/env ergo

# Compile function, taking one argument indicating the file to compile.
compile = fn source-file -> value cache seq ^[
    object-file = path new
    task (string format "compiling {}" $source-file) exec ^{env = {PATH=$}} c++ -o $object-file -c (fs track $source-file)
    $object-file
]

# Executable-linking function, which links all arguments.
link_exe = fn name ^objs -> value cache seq ^[
    exe = path new
    task (string format "linking executable {}" $name) exec ^{env = {PATH=$}} c++ -o $exe ^$objs
    $exe
]

# Create a list of files
files = [
    main.cpp
    lib.cpp
]

# Copy to a file in the current directory.
fs copy (link_exe forty_two ^(collection map $compile $files)) (path join $work-dir forty_two)
