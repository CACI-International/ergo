plugin = ergo plugin

## Download a file over HTTP or HTTPS.
##
## Arguments: `(String :url) (Path :target)`
##
## Keyed Arguments: All keyed arguments supported by `std:net:http`.
##
## Returns a Unit value, or an error if a non-successful status code is received.
download = fn (plugin:type:String :url) (plugin:type:Path :target) ^:rest -> {
    result = plugin:net:http :url ^:rest
    result:complete
    plugin:fs:write :target result:body
}

## Open a remote archive file.
##
## All arguments are forwarded to `std:net:http`.
##
## The url can resolve to any content that `fs:unarchive` accepts.
## Caches the result, only downloading a particular url once.
##
## Returns the unpacked remote archive directory.
unarchive-remote = fn ^:args -> plugin:value:cache {
    unpacked-dir = plugin:path:new:
    archive = plugin:net:http ^:args |>:body
    plugin:fs:unarchive :unpacked-dir :archive
    :unpacked-dir
}

{download, unarchive-remote}
