^workspace:

## Get the summary documentation of a value.
##
## Arguments: `:value`
##
## The summary documentation is the first paragraph of documentation (where paragraphs are separated
## by two newlines).
summary = fn :val -> (string:split "\n\n" (doc :val)):0

## Document a module.
##
## Arguments: `:value :module-index`
##
## The module must be accessible as an index `module-index` of `value`.
## If generating documentation, this will generate the module documentation separately and provide a
## link. It will display a summary of the module documentation.
module = fn :self :name -> {
    first = !match doc:path: ^[
        type:Unset -> :name
        _ -> string:format "[{}]({})" :name (doc:child :name self::name)
    ]
    string:format "{}: {}" :first (summary self::name)
}

## Document module indices of a value.
##
## Arguments: `:value ^:modules`
##
## Document multiple modules as an unordered list, with links to separate pages for each module if
## generating documentation. `modules` must be indices of `value`.
modules = fn :self ^:names -> {
    iter:map (fn :n -> string:format "* {}" <| module :self :n) :names | string:join "\n"
}

## Document function indices of a value.
##
## Arguments: `:value ^:functions`
##
## Document multiple functions as an unordered list with summaries, or as inline sections if
## generating documentation. `functions` must be indices of `value`.
functions = fn :self ^:names -> {
    !match doc:path: ^[
        type:Unset -> {
            iter:map (fn :n -> string:format "* {}: {}" :n (summary self::n)) :names | string:join "\n"
        }
        _ -> {
            # Build an unordered list of links to the functions
            list = iter:map (fn :n -> string:format "* [{0}](#{0}): {1}" :n (summary self::n)) :names | string:join "\n"
            content = iter:map (fn :n -> string:format "<div class='function'>\n\n## {}\n{}\n\n</div>" :n (doc self::n)) :names | string:join "\n\n"
            string:format "{}\n\n{}" :list :content
        }
    ]
}
